module Solcore.Frontend.TypeInference.TcEnv where

import Data.Map (Map)
import Data.Map qualified as Map
import Data.Set (Set)
import Data.Set qualified as Set
import Solcore.Desugarer.UniqueTypeGen (UniqueTyMap)
import Solcore.Frontend.Syntax
import Solcore.Frontend.TypeInference.Id
import Solcore.Frontend.TypeInference.NameSupply
import Solcore.Frontend.TypeInference.TcSubst
import Solcore.Pipeline.Options
import Solcore.Primitives.Primitives

-- definition of type environment
type Arity = Int

-- type constructor arity and names of constructors
data TypeInfo
  = TypeInfo
  { arity :: Arity, -- number of type parameters
    constrNames :: [Name], -- list of data constructor names
    fieldNames :: [Name] -- list of field names
  }
  deriving (Eq, Ord, Show)

-- type synonym information
data SynInfo
  = SynInfo
  { synArity :: Arity, -- number of type parameters
    synParams :: [Tyvar], -- type variable parameters
    synExpansion :: Ty -- the expanded type body
  }
  deriving (Eq, Ord, Show)

wordTypeInfo :: TypeInfo
wordTypeInfo = TypeInfo 0 [] []

unitTypeInfo :: TypeInfo
unitTypeInfo = TypeInfo 0 [Name "()"] []

pairTypeInfo :: TypeInfo
pairTypeInfo = TypeInfo 2 [Name "pair"] []

arrowTypeInfo :: TypeInfo
arrowTypeInfo = TypeInfo 2 [] []

boolTypeInfo :: TypeInfo
boolTypeInfo = TypeInfo 0 [trueName, falseName] []

sumTypeInfo :: TypeInfo
sumTypeInfo = TypeInfo 2 [inlName, inrName] []

stringTypeInfo :: TypeInfo
stringTypeInfo = TypeInfo 0 [] []

-- name of constructor and its scheme
type ConInfo = (Name, Scheme)

-- number of weak parameters and method names
type Method = Name

data ClassInfo
  = ClassInfo
  { classArity :: Arity,
    methods :: [Method],
    classpred :: Pred,
    supers :: [Pred]
  }
  deriving (Show)

type Table a = Map Name a

-- typing environment
type Env = Table Scheme

type ClassTable = Table ClassInfo

type TypeTable = Table TypeInfo

type SynTable = Table SynInfo

type Inst = Qual Pred

type InstTable = Table [Inst]

type DefTable = Table Inst

type InstanceHead = (Bool, Name, [Ty], Ty)

data TcEnv
  = TcEnv
  { ctx :: Env, -- Variable environment
    constrCtx :: Env, -- Primitive constructor schemes (never shadowed by user definitions)
    instEnv :: InstTable, -- Instance Environment
    defaultEnv :: DefTable, -- Default instance environment
    typeTable :: TypeTable, -- Type information environment
    synTable :: SynTable, -- Type synonym environment
    classTable :: ClassTable, -- Class information table
    contract :: Maybe Name, -- current contract name
    -- used to type check calls.
    subst :: Subst, -- Current substitution
    nameSupply :: NameSupply, -- Fresh name supply
    uniqueTypes :: UniqueTyMap, -- unique type map
    directCalls :: [Name], -- defined function names
    generateDefs :: Bool, -- should generate new defs?
    generated :: [TopDecl Id],
    counter :: Int, -- used to generate new names
    logs :: [String], -- Logging
    warnings :: [String], -- warnings collected to user
    enableLog :: Bool, -- Enable logging?
    coverage :: PragmaStatus, -- Disable coverage checking for names.
    patterson :: PragmaStatus, -- Disable Patterson condition for names.
    boundVariable :: PragmaStatus, -- Disable bound variable condition for names.
    trustedInstanceHeads :: [InstanceHead], -- Imported instances trusted by module boundary.
    partialDataTypes :: Set Name, -- Data types with hidden constructors in the current module.
    partialDataTypeConstructors :: Map Name (Set Name), -- Visible constructors for imported partial data types.
    maxRecursionDepth :: Int, -- max recursion depth in
    -- context reduction
    tcOptions :: Option
  }

initTcEnv :: Option -> TcEnv
initTcEnv opts =
  TcEnv
    { ctx = primCtx,
      constrCtx = primConstrCtx,
      instEnv = primInstEnv,
      defaultEnv = Map.empty,
      typeTable = primTypeEnv,
      synTable = Map.empty,
      classTable = primClassEnv,
      contract = Nothing,
      subst = mempty,
      nameSupply = namePool,
      uniqueTypes = primDataType,
      directCalls =
        [ Name "primAddWord",
          Name "primEqWord",
          QualName invokableName "invoke"
        ],
      generateDefs = True,
      generated = [],
      counter = 0,
      logs = [],
      warnings = [],
      enableLog = True,
      coverage = Enabled,
      patterson = Enabled,
      boundVariable = Enabled,
      trustedInstanceHeads = [],
      partialDataTypes = Set.empty,
      partialDataTypeConstructors = Map.empty,
      maxRecursionDepth = 100000,
      tcOptions = opts
    }

primCtx :: Env
primCtx =
  Map.fromList
    [ primAddWord,
      primEqWord,
      primInvoke,
      primPair,
      primUnit,
      primTrue,
      primFalse,
      primInvoke,
      wordToInteger,
      wordFromInteger,
      integerAdd,
      integerSub,
      integerMul,
      integerLt,
      integerEq,
      fromIntegerEntry,
      fromStringEntry
    ]

-- Primitive constructor schemes only — never overwritten by user function definitions.
-- Used by the type checker for Con/PCon lookups so that user functions named after
-- primitive constructors (e.g. "pair") cannot shadow them.
primConstrCtx :: Env
primConstrCtx =
  Map.fromList
    [ primPair,
      primUnit,
      primTrue,
      primFalse,
      primInl,
      primInr
    ]

primTypeEnv :: TypeTable
primTypeEnv =
  Map.fromList
    [ (Name "word", wordTypeInfo),
      (Name "pair", pairTypeInfo),
      (Name "->", arrowTypeInfo),
      (Name "()", unitTypeInfo),
      (Name "bool", boolTypeInfo),
      (Name "sum", sumTypeInfo),
      (Name "integer", TypeInfo 0 [] [])
    ]

primInstEnv :: InstTable
primInstEnv =
  Map.fromList
    [ ( intClassName,
        [ [] :=> InCls intClassName word [],
          [] :=> InCls intClassName integer []
        ]
      ),
      ( strClassName,
        [ [] :=> InCls strClassName string [],
          [] :=> InCls strClassName memString []
        ]
      )
    ]

primClassEnv :: ClassTable
primClassEnv =
  Map.fromList
    [ (Name "invokable", invokableInfo),
      (intClassName, intInfo),
      (strClassName, strInfo)
    ]
  where
    invokableInfo =
      ClassInfo
        2
        [QualName (Name "invokable") "invoke"]
        (InCls (Name "invokable") self args)
        []
    self = TyVar (TVar (Name "self"))
    args = map TyVar [TVar (Name "args"), TVar (Name "ret")]
    intInfo =
      ClassInfo
        0
        [QualName intClassName "fromInteger"]
        (InCls intClassName (TyVar (TVar (Name "a"))) [])
        []
    strInfo =
      ClassInfo
        0
        [QualName strClassName "fromString"]
        (InCls strClassName (TyVar (TVar (Name "a"))) [])
        []

primDataType :: Map Name DataTy
primDataType =
  Map.fromList
    [ (Name "primAddWord", dt1),
      (Name "primEqWord", dt2),
      (QualName (Name "invokable") "invoke", dt3)
    ]
  where
    dt1 =
      DataTy
        (Name "t_primAddWord")
        []
        [Constr (Name "t_primAddWord") [] []]
        []
    dt2 =
      DataTy
        (Name "t_primEqWord")
        []
        [Constr (Name "t_primEqWord") [] []]
        []
    dt3 =
      DataTy
        (Name "t_invokable.invoke")
        []
        [Constr (Name "t_invokable.invoke") [] []]
        []
