{-# OPTIONS_GHC -Wno-orphans #-}

module Solcore.Frontend.Pretty.TreePretty where

import Common.Pretty
import Data.List.NonEmpty qualified as N
import Solcore.Frontend.Syntax.Name
import Solcore.Frontend.Syntax.SyntaxTree

pretty :: (Pretty a) => a -> String
pretty = render . ppr

instance Pretty CompUnit where
  ppr (CompUnit imps cs) =
    vcat (map ppr imps ++ map ppr cs)

instance Pretty Import where
  ppr (ImportModule path) =
    text "import" <+> ppr path <+> semi
  ppr (ImportAlias path asName) =
    hsep [text "import", ppr path, text "as", ppr asName, semi]
  ppr (ImportOnly path items) =
    hsep
      [ text "import",
        ppr path <> text ".",
        pprItemSelector items <> semi
      ]

instance Pretty ModulePath where
  ppr (RelativePath path) = ppr path
  ppr (LibraryPath path) = text "lib." <> ppr path
  ppr (ExternalPath libName path) =
    text "@" <> ppr libName <> text "." <> ppr path

instance Pretty TopDecl where
  ppr (TContr c) = ppr c
  ppr (TFunDef fd) = ppr fd
  ppr (TClassDef c) = ppr c
  ppr (TInstDef is) = ppr is
  ppr (TDataDef d) = ppr d
  ppr (TSym s) = ppr s
  ppr (TExportDecl e) = ppr e
  ppr (TPragmaDecl p) = ppr p

instance Pretty Export where
  ppr (ExportList items) =
    hsep
      [ text "export",
        pprExportSpecs items <> semi
      ]
  ppr (ExportModule path) =
    hsep [text "export", ppr path <> semi]
  ppr (ExportModuleAs path asName) =
    hsep [text "export", ppr path, text "as", ppr asName <> semi]
  ppr (ExportItemsFrom path items)
    | exportSelectorIsOnlyWildcard items =
        hsep [text "export", ppr path <> text ".*;"]
    | otherwise =
        hsep [text "export", ppr path <> text ".", pprExportSelector items <> semi]

pprExportSpecs :: [ExportSpec] -> Doc
pprExportSpecs items = lbrace <> commaSep (map ppr items) <> rbrace

instance Pretty ExportSpec where
  ppr ExportAll = text "*"
  ppr (ExportName itemName) = ppr itemName
  ppr (ExportNameWithConstructors typeName ctorSelector) =
    ppr typeName <> parens (ppr ctorSelector)
  ppr (ExportModuleAll path) = ppr path <> text ".*"

instance Pretty ConstructorSelector where
  ppr SelectAllConstructors = text "*"
  ppr (SelectConstructors names) = commaSep (map ppr names)

pprExportSelector :: ExportSelector -> Doc
pprExportSelector (SelectExportItems items) =
  lbrace <> commaSep (map ppr items) <> rbrace

instance Pretty ExportSelectorEntry where
  ppr SelectExportAllItems = text "*"
  ppr (SelectExportItem itemName) = ppr itemName
  ppr (SelectExportConstructors typeName ctorSelector) =
    ppr typeName <> parens (ppr ctorSelector)

pprItemSelector :: ItemSelector -> Doc
pprItemSelector (SelectItems items hidden) =
  base <> pprHiding hidden
  where
    base = lbrace <> commaSep (map ppr items) <> rbrace
    pprHiding [] = empty
    pprHiding names =
      space <> (text "hiding" <+> (lbrace <> commaSep (map ppr names) <> rbrace))

instance Pretty ItemSelectorEntry where
  ppr SelectAllItems = text "*"
  ppr (SelectItem itemName) = ppr itemName
  ppr (SelectItemAs itemName aliasName) =
    hsep [ppr itemName, text "as", ppr aliasName]

exportSelectorIsOnlyWildcard :: ExportSelector -> Bool
exportSelectorIsOnlyWildcard (SelectExportItems [SelectExportAllItems]) = True
exportSelectorIsOnlyWildcard _ = False

instance Pretty Pragma where
  ppr (Pragma _ Enabled) = empty
  ppr (Pragma ty st) =
    hsep [text "pragma", ppr ty, ppr st, semi]

instance Pretty PragmaType where
  ppr NoBoundVariableCondition = text "no-bounded-variable-condition"
  ppr NoCoverageCondition = text "no-coverage-condition"
  ppr NoPattersonCondition = text "no-patterson-condition"
  ppr NoGenericInstanceFor = text "no-generic-instance-for"

instance Pretty PragmaStatus where
  ppr (DisableFor ns) =
    commaSep (map ppr $ N.toList ns)
  ppr _ = empty

instance Pretty Contract where
  ppr (Contract n ts ds) =
    text "contract"
      <+> ppr n
      <+> pprTyParams ts
      <+> lbrace
      $$ nest 3 (vcat (map ppr ds))
      $$ rbrace

instance Pretty ContractDecl where
  ppr (CDataDecl dt) =
    ppr dt
  ppr (CFieldDecl fd) =
    ppr fd
  ppr (CFunDecl fd) =
    ppr fd
  ppr (CConstrDecl c) =
    ppr c

instance Pretty Constructor where
  ppr (Constructor ps bd payable) =
    (if payable then text "payable" <+> text "constructor" else text "constructor")
      <+> pprParams ps
      <+> lbrace
      $$ nest 3 (vcat (map ppr bd))
      $$ rbrace

instance Pretty DataTy where
  ppr (DataTy n ps cs) =
    text "data"
      <+> ppr n
      <+> pprTyParams ps
      <+> rs
      <+> text ";"
    where
      rs =
        if null cs
          then empty
          else
            equals <+> hsep (punctuate bar (map ppr cs))
      bar = text " |"

instance Pretty TySym where
  ppr (TySym n vs t) =
    text "type"
      <+> ppr n
      <+> pprTyParams vs
      <+> text "="
      <+> ppr t

instance Pretty Constr where
  ppr (Constr n []) = ppr n <> text " "
  ppr (Constr n ts) =
    ppr n <> parens (pprConstrArgs ts)

pprConstrArgs :: [Ty] -> Doc
pprConstrArgs [] = empty
pprConstrArgs ts = commaSep $ map ppr ts

instance Pretty Class where
  ppr (Class bvs ps n vs v sigs) =
    pprSigPrefix bvs ps
      <+> text "class "
      <+> ppr v
      <+> colon
      <+> ppr n
      <+> pprTyParams vs
      <+> lbrace
      $$ nest 3 (pprSignatures sigs)
      $$ rbrace

pprSignatures :: [Signature] -> Doc
pprSignatures =
  vcat . map ((<> semi) . ppr)

instance Pretty Signature where
  ppr (Signature vs ctx n ps rc ty pay) =
    pprSigPrefix vs ctx
      <+> (if pay then text "payable" else empty)
      <+> text "function"
      <+> ppr n
      <+> pprParams ps
      <+> pprRetTy rc ty

pprSigPrefix :: [Ty] -> [Pred] -> Doc
pprSigPrefix [] [] = empty
pprSigPrefix [] ps = pprContext ps
pprSigPrefix vs [] =
  text "forall" <+> hsep (map ppr vs) <+> text "."
pprSigPrefix vs ps =
  text "forall" <+> hsep (map ppr vs) <+> text "." $$ pprContext ps

instance Pretty Instance where
  ppr (Instance d vs ctx n tys ty funs) =
    pprSigPrefix vs ctx
      <+> pprDefault d
      <+> text "instance"
      <+> ppr ty
      <+> colon
      <+> ppr n
      <+> pprTyParams tys
      <+> lbrace
      $$ nest 3 (pprFunBlock funs)
      $$ rbrace

pprDefault :: Bool -> Doc
pprDefault b = if b then text "default " else empty

pprContext :: [Pred] -> Doc
pprContext [] = empty
pprContext ps =
  (commaSep $ map ppr ps) <+> text "=>"

instance Pretty [Pred] where
  ppr = parens . commaSepList

pprFunBlock :: [FunDef] -> Doc
pprFunBlock =
  vcat . map ppr

instance Pretty Field where
  ppr (Field n ty e) =
    ppr n <+> colon <+> (ppr ty) <+> pprInitOpt e

instance Pretty Body where
  ppr = vcat . map ppr

instance Pretty FunDef where
  ppr (FunDef isPub sig bd) =
    ((if isPub then text "public " else empty) <> ppr sig)
      <+> lbrace
      $$ nest 3 (vcat (map ppr bd))
      $$ rbrace

pprRetTy :: Bool -> Maybe Ty -> Doc
pprRetTy _ Nothing = empty
pprRetTy rc (Just t) = text "->" <+> (pprConst rc <> ppr t)

pprParams :: [Param] -> Doc
pprParams = parens . commaSep . map ppr

pprConst :: Bool -> Doc
pprConst True = text "comptime "
pprConst False = empty

instance Pretty Param where
  ppr (Typed c n ty) =
    pprConst c <> (ppr n <+> colon <+> ppr ty)
  ppr (Untyped c n) =
    pprConst c <> ppr n

instance Pretty Stmt where
  ppr (Assign n e) =
    ppr n <+> equals <+> ppr e <+> semi
  ppr (StmtPlusEq e1 e2) =
    hsep [ppr e1, text "+=", ppr e2]
  ppr (StmtMinusEq e1 e2) =
    hsep [ppr e1, text "-=", ppr e2]
  ppr (StmtXorEq e1 e2) =
    hsep [ppr e1, text "^=", ppr e2]
  ppr (StmtBAndEq e1 e2) =
    hsep [ppr e1, text "&=", ppr e2]
  ppr (StmtBOrEq e1 e2) =
    hsep [ppr e1, text "|=", ppr e2]
  ppr (StmtModEq e1 e2) =
    hsep [ppr e1, text "%=", ppr e2]
  ppr (Let c n ty m) =
    text "let" <+> ppr n <+> pprOptTy c ty <+> pprInitOpt m
  ppr (Block body) =
    lbrace
      $$ nest 3 (ppr body)
      $$ rbrace
  ppr (StmtExp e) =
    ppr e <> semi
  ppr (Return e) =
    text "return" <+> ppr e <+> semi
  ppr (Match e eqns) =
    text "match"
      <+> (parens $ commaSep $ map ppr e)
      <+> lbrace
      $$ vcat (map ppr eqns)
      $$ rbrace
  ppr (Asm yblk) =
    text "assembly"
      <+> lbrace
      $$ nest 3 (vcat (map ppr yblk))
      $$ rbrace
  ppr (If e blk1 blk2) =
    text "if"
      <+> parens (ppr e)
      <+> lbrace
      $$ nest 3 (ppr blk1)
      $$ rbrace
      <+> text "else"
      <+> lbrace
      $$ nest 3 (ppr blk2)
      $$ rbrace
  ppr (For initStmt cond postStmt body) =
    text "for"
      <+> parens (hsep [pprForClause initStmt <> semi, ppr cond <> semi, pprForClause postStmt])
      <+> lbrace
      $$ nest 3 (ppr body)
      $$ rbrace
  ppr Break = text "break" <> semi
  ppr EmptyStmt = empty

pprForClause :: Stmt -> Doc
pprForClause (Assign n e) = ppr n <+> equals <+> ppr e
pprForClause (StmtPlusEq e1 e2) = hsep [ppr e1, text "+=", ppr e2]
pprForClause (StmtMinusEq e1 e2) = hsep [ppr e1, text "-=", ppr e2]
pprForClause (StmtXorEq e1 e2) = hsep [ppr e1, text "^=", ppr e2]
pprForClause (StmtBAndEq e1 e2) = hsep [ppr e1, text "&=", ppr e2]
pprForClause (StmtBOrEq e1 e2) = hsep [ppr e1, text "|=", ppr e2]
pprForClause (StmtModEq e1 e2) = hsep [ppr e1, text "%=", ppr e2]
pprForClause (Let ct n ty m) = text "let" <+> ppr n <+> pprOptTy ct ty <+> pprForInitOpt m
pprForClause (StmtExp e) = ppr e
pprForClause EmptyStmt = empty
pprForClause s = ppr s

pprForInitOpt :: Maybe Exp -> Doc
pprForInitOpt Nothing = empty
pprForInitOpt (Just e) = equals <+> ppr e

instance Pretty Equation where
  ppr (p, ss) =
    text "|"
      <+> commaSep (map ppr p)
      <+> text "=>"
      $$ nest 3 (vcat (map ppr ss))

instance Pretty Equations where
  ppr = vcat . map ppr

pprOptTy :: Bool -> Maybe Ty -> Doc
pprOptTy _ Nothing = empty
pprOptTy c (Just t) =
  case splitTy t of
    ([], t') -> text ":" <+> (pprConst c <> ppr t')
    (ts', t') ->
      text ":"
        <+> parens (commaSep (map ppr ts'))
        <+> text "->"
        <+> ppr t'

pprInitOpt :: Maybe Exp -> Doc
pprInitOpt Nothing = semi
pprInitOpt (Just e) = equals <+> ppr e <+> semi

parensWhen :: Bool -> Doc -> Doc
parensWhen True d = parens d
parensWhen _ d = d

instance Pretty Exp where
  ppr (Lit l) = ppr l
  ppr (ExpName me n es) =
    maybe empty (\e -> ppr e <> char '.') me
      <> ppr n
      <> parensWhen
        (not $ null es)
        (commaSep (map ppr es))
  ppr (ExpVar me v) =
    maybe empty (\e -> ppr e <> char '.') me
      <> ppr v
  ppr (ExpDotName n es) =
    char '.'
      <> ppr n
      <> parensWhen
        (not $ null es)
        (commaSep (map ppr es))
  ppr (Lam args bd _) =
    text "lam"
      <+> pprParams args
      <+> lbrace
      $$ nest 3 (vcat (map ppr bd))
      $$ rbrace
  ppr (TyExp e ty) =
    ppr e <+> text ":" <+> ppr ty
  ppr (ExpIndexed e1 e2) =
    ppr e1 <> brackets (ppr e2)
  ppr (ExpPlus e1 e2) =
    hsep [ppr e1, text "+", ppr e2]
  ppr (ExpMinus e1 e2) =
    hsep [ppr e1, text "-", ppr e2]
  ppr (ExpTimes e1 e2) =
    hsep [ppr e1, text "*", ppr e2]
  ppr (ExpDivide e1 e2) =
    hsep [ppr e1, text "/", ppr e2]
  ppr (ExpModulo e1 e2) =
    hsep [ppr e1, text "%", ppr e2]
  ppr (ExpXor e1 e2) =
    hsep [ppr e1, text "^", ppr e2]
  ppr (ExpBAnd e1 e2) =
    hsep [ppr e1, text "&", ppr e2]
  ppr (ExpBOr e1 e2) =
    hsep [ppr e1, text "|", ppr e2]
  ppr (ExpLT e1 e2) =
    hsep [ppr e1, text "<", ppr e2]
  ppr (ExpGT e1 e2) =
    hsep [ppr e1, text ">", ppr e2]
  ppr (ExpLE e1 e2) =
    hsep [ppr e1, text "<=", ppr e2]
  ppr (ExpGE e1 e2) =
    hsep [ppr e1, text ">=", ppr e2]
  ppr (ExpEE e1 e2) =
    hsep [ppr e1, text "==", ppr e2]
  ppr (ExpNE e1 e2) =
    hsep [ppr e1, text "!=", ppr e2]
  ppr (ExpLAnd e1 e2) =
    hsep [ppr e1, text "&&", ppr e2]
  ppr (ExpLOr e1 e2) =
    hsep [ppr e1, text "||", ppr e2]
  ppr (ExpLNot e1) =
    hsep [text "!", ppr e1]
  ppr (ExpCond e1 e2 e3) =
    hsep
      [ text "if",
        ppr e1,
        text "then",
        ppr e2,
        text "else",
        ppr e3
      ]
  ppr (ExpAt t) =
    text "@" <> ppr t

pprE :: Maybe Exp -> Doc
pprE Nothing = ""
pprE (Just e) = ppr e <> text "."

instance Pretty Pat where
  ppr (Pat n []) = ppr n
  ppr (Pat n ps@(_ : _))
    | isTuple n = parens (commaSep $ map ppr ps)
    | otherwise = ppr n <> (parens $ commaSep $ map ppr ps)
  ppr (PatDot n []) =
    char '.' <> ppr n
  ppr (PatDot n ps@(_ : _)) =
    char '.' <> ppr n <> (parens $ commaSep $ map ppr ps)
  ppr PWildcard =
    text "_"
  ppr (PLit l) =
    ppr l
  ppr (PExp e) =
    text "comptime" <+> ppr e

instance Pretty Literal where
  ppr (IntLit l) = integer (toInteger l)
  ppr (StrLit l) = quotes (text l)

instance Pretty Pred where
  ppr (InCls n t ts) =
    ppr t <+> colon <+> ppr n <+> pprTyParams ts

instance Pretty Ty where
  ppr (t1@(_ :-> _) :-> t2) =
    parens (ppr t1) <+> text "->" <+> ppr t2
  ppr (t1 :-> t2) =
    ppr t1 <+> (text "->") <+> ppr t2
  ppr (TyCon n ts)
    | isTuple n = parens $ commaSep (map ppr ts)
    | isUnit n = text "()"
    | otherwise = ppr n <> (pprTyParams ts)

isUnit :: Name -> Bool
isUnit n = pretty n == "unit"

isTuple :: (Pretty a) => a -> Bool
isTuple s = pretty s == "pair"

pprTyParams :: [Ty] -> Doc
pprTyParams [] = empty
pprTyParams ts =
  parens (commaSep (map ppr ts))

splitTy :: Ty -> ([Ty], Ty)
splitTy (a :-> b) =
  let (as, r) = splitTy b
   in (a : as, r)
splitTy t = ([], t)
