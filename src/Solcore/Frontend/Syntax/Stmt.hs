module Solcore.Frontend.Syntax.Stmt where

import Data.Generics (Data, Typeable)
import Language.Yul
import Solcore.Frontend.Syntax.Ty

-- definition of statements

type Equation a = ([Pat a], [Stmt a])

type Equations a = [Equation a]

data Stmt a
  = (Exp a) := (Exp a) -- assignment
  | Let Bool a (Maybe Ty) (Maybe (Exp a)) -- local variable; Bool is True when 'comptime' modifier is present
  | Block (Body a) -- lexical block
  | StmtExp (Exp a) -- expression level statements
  | Return (Exp a) -- return statements
  | Match [Exp a] (Equations a) -- pattern matching
  | Asm YulBlock -- Yul block
  | If (Exp a) (Body a) (Body a) -- If statement
  | For (Stmt a) (Exp a) (Stmt a) (Body a) -- for(init; cond; post) { body }
  | Break -- break out of the innermost enclosing for loop
  | Continue -- continue to the next iteration of the innermost enclosing for loop
  | EmptyStmt -- empty statement (for empty for init/post)
  deriving (Eq, Ord, Show, Data, Typeable)

type Body a = [Stmt a]

data Param a
  = Typed Bool a Ty -- Bool is True when 'const' modifier is present
  | Untyped Bool a
  deriving (Eq, Ord, Show, Data, Typeable)

paramName :: Param a -> a
paramName (Typed _ n _) = n
paramName (Untyped _ n) = n

paramComptime :: Param a -> Bool
paramComptime (Typed ct _ _) = ct
paramComptime (Untyped ct _) = ct

-- definition of the expression syntax

data Exp a
  = Var a -- variable
  | Con a [Exp a] -- data type constructor
  | FieldAccess (Maybe (Exp a)) a -- field access
  | Lit Literal -- literal
  | Call (Maybe (Exp a)) a [Exp a] -- function call
  | Lam [Param a] (Body a) (Maybe Ty) -- lambda-abstraction
  | TyExp (Exp a) Ty -- type annotated expression
  | Cond (Exp a) (Exp a) (Exp a) -- conditional expression
  | Indexed (Exp a) (Exp a) -- e1[e2]
  deriving (Eq, Ord, Show, Data, Typeable)

-- pattern matching equations

data Pat a
  = PVar a
  | PCon a [Pat a]
  | PWildcard
  | PLit Literal
  | PExp (Exp a) -- comptime expression label (numeric matches only)
  deriving (Eq, Ord, Show, Data, Typeable)

-- definition of literals

data Literal
  = IntLit Integer
  | StrLit String
  deriving (Eq, Ord, Show, Data, Typeable)
